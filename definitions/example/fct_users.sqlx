config {
    type: "incremental",
    description: "Adultes users table",
    columns: {
        id: "User's identifier",
        first_name: "User's first name",
        last_name: "User's last name",
        email: "User's email",
        gender: "User's gender",
        birth_date: "User's birth date",
        creation_timestamp: "Users's creation timestamp",
        update_timestamp: "Users's data update timestamp"
    },
    tags: ["FACT"],
    bigquery: {
        labels: {
            "type": "fact"
        },
        partitionBy: "DATE(update_timestamp)",
        requirePartitionFilter: true,
        clusterBy: ["gender"]
    },
    dependencies: ["adultes_users_assertion", "labs_stg_users_assertions_rowConditions", "labs_stg_users_assertions_uniqueKey_0"]
}

SELECT
  *
FROM
  ${ref("stg_users")}
  ${
      when(incremental(),
          `WHERE TIMESTAMP_TRUNC(update_timestamp, DAY) >= TIMESTAMP_SUB(current_timestamp(), INTERVAL 1 DAY)`,
          `WHERE TIMESTAMP_TRUNC(update_timestamp, DAY) > timestamp("1970-01-01")`
      )
  }
